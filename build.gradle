buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'net.ltgt.gradle:gradle-apt-plugin:0.21'
    }
}

rootProject.version = '9.7.0'
group = 'com.spaceshift'

allprojects {

    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    ext {
        projectreactorVersion = '3.2.9.RELEASE'
        sl4jVersion = '1.7.26'
        javaxMailVersion = "1.5.0-b01"
        testcontainersVersion = '1.11.1'
        lombokVersion = '1.18.6'
        junitJupiterVersion = "5.4.2"
        jetbrainsAnnotation = '17.0.0'
        bintrayVersion = version
    }

    javadoc {
        failOnError = false
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:$jetbrainsAnnotation"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    
        testCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.jetbrains:annotations:$jetbrainsAnnotation"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }

    /*compileJava {
        inputs.property("moduleName", jar.baseName)
        doFirst {
            options.compilerArgs = [
                '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }*/

    task sourcesJar(type: Jar, dependsOn: classes) {
        afterEvaluate {
            getArchiveClassifier().set("sources")
            getArchiveBaseName().set(jar.getArchiveBaseName())
            from sourceSets.main.allSource
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        afterEvaluate {
            getArchiveClassifier().set("javadoc")
            getArchiveBaseName().set(jar.getArchiveBaseName())
            from sourceSets.main.allSource
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                version = rootProject.version
                afterEvaluate {
                    artifactId = jar.baseName
                    groupId = rootProject.group
                }
            }
        }
    }

    bintray {
        afterEvaluate {
            user = project.hasProperty("bintrayUser") ? project.property("bintrayUser") : ""
            key = project.hasProperty("bintrayKey") ?  project.property("bintrayKey") : ""
            publications = ['mavenJava']
            pkg {
                repo = 'maven'
                name = 'com.spaceshift.' + jar.baseName
                licenses = ['Apache-2.0']
                websiteUrl = 'https://github.com/JavaSaBr/Rlib'
                vcsUrl = 'https://github.com/JavaSaBr/RLib.git'
                issueTrackerUrl = 'https://github.com/JavaSaBr/RLib/issues'
                githubRepo = 'JavaSaBr/RLib'
                githubReleaseNotesFile = 'README.md'
                version {
                    name = rootProject.version
                    desc = rootProject.version
                    released = new Date()
                }
            }
        }
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }

    task testJar(type: Jar) {
        getArchiveClassifier().set("test")
        from sourceSets.test.output
    }

    artifacts {
        testArtifacts testJar
    }

    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors()
    }
}

wrapper {
    gradleVersion = '6.0'
    distributionType = Wrapper.DistributionType.ALL
}
