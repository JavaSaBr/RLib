buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.19"
    }
}

rootProject.version = "9.1.0"
group = "com.spaceshift"

allprojects {

    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: "java"
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    ext {
        lombokVersion = '1.18.4'
        junitJupiterVersion = "5.2.0"
        jetbrainsAnnotation = '16.0.3'
        bintrayVersion = version
    }

    javadoc {
        failOnError = false
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }

        reports {
            html.enabled = true
        }
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:$jetbrainsAnnotation"
        testCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
        testCompileOnly "org.jetbrains:annotations:$jetbrainsAnnotation"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        afterEvaluate {
            classifier = 'sources'
            baseName = jar.baseName
            from sourceSets.main.allSource
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        afterEvaluate {
            classifier = 'javadoc'
            baseName = jar.baseName
            from sourceSets.main.allSource
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                version = rootProject.version
                afterEvaluate {
                    artifactId = jar.baseName
                    groupId = rootProject.group
                }
            }
        }
    }

    bintray {
        afterEvaluate {
            user = System.getProperty('bintrayUser')
            key = System.getProperty('bintrayKey')
            publications = ['mavenJava']
            pkg {
                repo = 'maven'
                name = 'com.spaceshift.' + jar.baseName
                licenses = ['Apache-2.0']
                websiteUrl = 'https://github.com/JavaSaBr/Rlib'
                vcsUrl = 'https://github.com/JavaSaBr/RLib.git'
                issueTrackerUrl = 'https://github.com/JavaSaBr/RLib/issues'
                githubRepo = 'JavaSaBr/RLib'
                githubReleaseNotesFile = 'README.md'
                version {
                    name = rootProject.version
                    desc = rootProject.version
                    released = new Date()
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '5.2'
    distributionType = Wrapper.DistributionType.ALL
}